extends Node

@export var mob_scene: PackedScene
@onready var player: Area2D = $Player

var score

func game_over():
 $ScoreTimer.stop()
 $MobTimer.stop()
 $HUD.show_game_over()


func new_game():
 score = 0
 player.start($StartPosition.position)
 $StartTimer.start()
 $HUD.update_score(score)
 $HUD.show_message("Get Ready")


func _on_start_timer_timeout():
 $MobTimer.start()
 $ScoreTimer.start()


func _on_mob_timer_timeout():

# Create a new instance of the mob scene.
 var mob = mob_scene.instantiate()

# Choose a random location on the Path2D.
 var mob_spawn_location = $MobPath/MobSpawnLocation
 mob_spawn_location.progress_ratio = randf()

# Set the mob's position.
 mob.position = mob_spawn_location.position

# Set the mob's direction perpendicular to the path.
 var direction = mob_spawn_location.rotation + PI / 2
# Add some randomness to its direction.
 direction += randf_range(-PI / 4, PI / 4)
 mob.rotation = direction
# Choose the velocity for the mob.
 var velocity = Vector2(randf_range(150.0, 250.0), 0.0)
 mob.linear_velocity = velocity.rotated(direction)

# Spawn the mob by adding it to the Main scene.
 add_child(mob)



func _on_score_timer_timeout():
 score += 1
 $HUD.update_score(score)
